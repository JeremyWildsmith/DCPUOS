#define SHELLINSTANCE_OFFSET_CUR_WRITEORIGIN 	0
#define SHELLINSTANCE_OFFSET_CUR_INPUTCURSOR	1
#define SHELLINSTANCE_OFFSET_INPUTBUFFER		2
#define SHELLINSTANCE_OFFSET_STATE				43
#define SHELLINSTANCE_SIZE 						450

#define SHELLINSTANCE_INPUTBUFFER_SIZE			41
#define SHELLINSTANCE_INPUTBUFFER_MAXINPUT		40 ;Last character is reserved for null-terminator

image_header:
header_signature:
	DAT 0x10C

header_next:
	DAT	0
	
header_name:
	DAT 0x6873, 0x6c65, 0x006c, 0, 0, 0, 0, 0, 0, 0 ; cstring of 'shell'
	
header_rva_ep:
	DAT 0
	
test_program_rva_import:
	DAT header_import_table
test_program_rva_export:
	DAT header_export_table
test_program_rva_offset:
	DAT header_relocations
test_program__imageSize:
	DAT 0x100
	
header_import_table:
	dat 0x656b, 0x6e72, 0x6c65, 0, 0, 0, 0, 0, 0, 0 ;cstring of 'kernel'
import_userInterfaceState_createState:
	DAT 6
import_userInterfaceState_destroyState:
	DAT 7
import_userInterfaceState_getVram:
	DAT 8
import_graphics_writeString:
	DAT 4
import_graphics_shiftVertical:
	DAT 5
	DAT 0xFFFF
	DAT 0xFFFF
	
header_export_table:
	DAT shell_create
	DAT shell_writeString
	DAT shell_processInput
	DAT shell_newLine
	DAT 0xFFFF
	
header_relocations:
	DAT 0xFFFF
	
clearLine:
	dat "                                ",0
	
;cdecl inits shell structure
;ARG1 pointer to SHELLINSTANCE
;ARG2 input callback
;ARG3 base address of caller module.
shell_create:
	_sfBegin(0)
	_sfArg(c, 0)
	_loadBase(a)
	
	SET [C + SHELLINSTANCE_OFFSET_CUR_WRITEORIGIN], 0
	SET [C + SHELLINSTANCE_OFFSET_CUR_INPUTCURSOR], 0
	
	_sfArg(push, 1)
	_sfArg(push, 2)
	SET PUSH, C
	ADD [SP], SHELLINSTANCE_OFFSET_STATE
	JSR [A + import_userInterfaceState_createState]
	ADD SP, 3
	
	_push z
	
	_loadBase(a)
	add a, clearLine
	set push, a
	_sfArg(push, 0)
	
	set z, 12
	writeNewLine:
		relCall shell_writeString
		sub z, 1
		ifn z, 0
			jmpU writeNewLine
	
	add sp, 2
	_pop z
	_sfEnd
	SET PC, POP

;cdecl 
;ARG1 is base of shell structure
;ARG2 is ptr to string to write
shell_writeString:
	_sfBegin(0)
	_loadBase(a)
	
	_sfArg(push, 0) ; Get address of VRAM
	add [SP], SHELLINSTANCE_OFFSET_STATE
	callRelPtr import_userInterfaceState_getVram
	add sp, 1
	
	_sfArg(y, 0)
	_push [y + SHELLINSTANCE_OFFSET_CUR_WRITEORIGIN]
	_sfArg(push, 1)
	_push a
	callRelPtr import_graphics_writeString
	add sp, 3
	
	_sfArg(y, 0)
	set [y + SHELLINSTANCE_OFFSET_CUR_WRITEORIGIN], a
	
	_sfEnd
	ret

;cdecl
;ARG1 is base of shell structure
shell_newLine:
	SET A, [SP+1]
	SET Y, [A + SHELLINSTANCE_OFFSET_CUR_WRITEORIGIN]
	MOD Y, GRAPHICS_SCREENX
	ADD [A + SHELLINSTANCE_OFFSET_CUR_WRITEORIGIN], GRAPHICS_SCREENX
	SUB [A + SHELLINSTANCE_OFFSET_CUR_WRITEORIGIN], Y
	SET PC, POP
	
;cdecl 
;ARG1 is base of shell structure
;ARG2 is keycode of key pressed
;Return if return is >0, then it is the address of a null-terminated string entered (when the user hit enter.) 
;The string is stored in a temporary buffer and is !!!only valid until this routine is called again.!!!
shell_processInput:
		_sfBegin(0)
		SET PUSH, I
	shell_processInput_nextInstruction:
		_loadBase(I)
		
		_sfArg(y, 0)
		
		SET C, I
		ADD C, shell_processInput_userReturned
		_sfArgIfEquals(1, KEYBOARD_KEY_RETURN)
			SET PC, C
		
		SET C, I
		ADD C, shell_processInput_backspace
		_sfArgIfEquals(1, KEYBOARD_KEY_BACKSPACE)
			SET PC, C
			
		SET C, I
		ADD C, shell_processInput_ignoreInput
		IFE [Y + SHELLINSTANCE_OFFSET_CUR_INPUTCURSOR], SHELLINSTANCE_INPUTBUFFER_MAXINPUT
			SET PC, C
		
		SET C, Y
		ADD C, [Y + SHELLINSTANCE_OFFSET_CUR_INPUTCURSOR]
		_sfArg([C + SHELLINSTANCE_OFFSET_INPUTBUFFER], 1)
		ADD [Y + SHELLINSTANCE_OFFSET_CUR_INPUTCURSOR], 1
		
	shell_processInput_writeCharacterToScreen:
		_push 0
		_sfArg(push, 1)
		_push sp
		_sfArg(y, 0)
		_push y
		set y, I
		add y, shell_writeString
		jsr y
		add sp, 4
		_pop i		
		
		xor a, a
		_sfEnd()
		ret
		
	shell_processInput_userReturned:
		_sfArg(y, 0)
		
		;null terminate string
		set x, [Y + SHELLINSTANCE_OFFSET_CUR_INPUTCURSOR]
		add x, y
		add x, SHELLINSTANCE_OFFSET_INPUTBUFFER
		set [x], 0
		
		set [Y + SHELLINSTANCE_OFFSET_CUR_INPUTCURSOR], 0
		
		_push y
		SET C, I
		ADD C, shell_newLine
		JSR C
		_pop y
		
		SET A, Y
		ADD A, SHELLINSTANCE_OFFSET_INPUTBUFFER
		SET I, POP
		_sfEnd()
		SET PC, POP
		
	shell_processInput_ignoreInput:
		XOR A, A
		SET I, POP
		_sfEnd()
		SET PC, POP
		
	shell_processInput_backspace:
		SET C, I
		ADD C, shell_processInput_ignoreInput
		IFE [Y + SHELLINSTANCE_OFFSET_CUR_INPUTCURSOR], 0
			SET PC, C
			
		SUB [Y + SHELLINSTANCE_OFFSET_CUR_INPUTCURSOR], 1
		SET PUSH, 0
		SET PUSH, KEYBOARD_KEY_SPACE
		SET C, SP
		
		SUB [Y + SHELLINSTANCE_OFFSET_CUR_WRITEORIGIN], 1
		SET PUSH, [Y + SHELLINSTANCE_OFFSET_CUR_WRITEORIGIN] ;;call to draw string stack setup
		SET PUSH, C
		
		SET PUSH, Y
		ADD [SP], SHELLINSTANCE_OFFSET_STATE
		JSR [I + import_userInterfaceState_getVram]
		ADD SP, 1
		
		SET PUSH, A ;Call to draw string stack setup.
		
		JSR [I + import_graphics_writeString]
		ADD SP, 5
		SET I, POP
		XOR A, A
		_sfEnd()
		SET PC, POP